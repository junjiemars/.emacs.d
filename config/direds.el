;; -*- lexical-binding:t -*-
;;;;
;; Nore Emacs
;; https://github.com/junjiemars/.emacs.d
;;;;
;; direds.el
;;;;

;;; require

(declare-function browse-url-default-browser "browse-url")
(declare-function dired-current-directory "dired")
(autoload 'dired-current-directory "dired")

;; end of require

;;; macro

(eval-when-compile
  (defmacro unless-default-file-name-coding-system% (&rest body)
    (declare (indent 0))
    `(unless% (eq default-file-name-coding-system locale-coding-system)
       ,@body)))

;; end of macro

(when-platform% windows-nt
  ;; on Windows: there are no builtin zip program
  ;; so try to use minzip in Emacs dep for Windows.
  ;; zip.bat works with `dired-do-compress-to' and `org-odt-export-to-odt'.
  (eval-when-compile

    (defun _make_zip_bat_ (zip &rest ignore)
      "Make ZIP.bat in \\=`exec-path\\=' for minizip or 7z[a]."
      (declare (indent 1))
      (when (stringp zip)
        (save-str-to-file
         (concat
          "@echo off\n"
          (format "REM zip.bat for %s on Windows\n" zip)
          "REM generated by Nore Emacs " (nore-emacs) "\n\n"
          "REM local variable declaration\n\n"
          "setlocal EnableDelayedExpansion\n"
          "\n"
          "set _OPT=%*\n"
          "set _ZIP=\n"
          "set _ARGV=\n"
          "\n"
          "REM parsing command line arguments\n\n"
          ":getopt\n"
          (cond ((string-equal "minizip" zip)
                 "if \"%1\"==\"-mX0\" set _OPT=%_OPT:-mX0=-0% & shift & goto :getopt\n")
                ((string-match "7za?" zip)
                 (concat
                  "if \"%1\"==\"-mX0\" set _OPT=%_OPT:-mX0=-mx0% & shift & goto :getopt\n"
                  "if \"%1\"==\"-0\" set _OPT=%_OPT:-0=-mx0% & shift & goto :getopt\n"
                  "if \"%1\"==\"-9\" set _OPT=%_OPT:-9=-mx9% & shift & goto :getopt\n")))
          "\n"
          "REM ignore options\n"
          (let ((options nil))
            (dolist (x (cond ((string-equal "minizip" zip)
                              (append '("-r" "--filesync" "-rmTq") ignore))
                             ((string-match "7za?" zip)
                              (append '("-r" "--filesync" "-rmTq"))))
                       options)
              (setq options
                    (concat options
                            (format "if \"%%1\"==\"%s\" set _OPT=%%_OPT:%s=%% & shift & goto :getopt\n" x x)))))
          "\n"
          "REM extract zip and argv\n"
          "if not \"%1\"==\"\" (\n"
          "  if \"%_ZIP%\"==\"\" (\n"
          "    if \"%_ARGV%\"==\"\" (\n"
          "      set _ZIP=%1\n"
          "    )\n"
          "  ) else (\n"
          "    set _ARGV=%_ARGV% %1\n"
          "  )\n"
          "  set _OPT=!_OPT:%1=!\n"
          "  shift\n"
          "  goto :getopt\n"
          ")\n\n"
          (cond ((string-match "7za?" zip)
                 (let ((7z (string-match* "7za?" zip 0)))
                   (format (concat "REM %s call\n"
                                   "%s a %%_OPT%% -tzip -- %%_ZIP%% %%_ARGV%%\n"
                                   "if exist %%_ZIP%% (\n"
                                   "  %s d %%_OPT%% -tzip -- %%_ZIP%% %%_ZIP%%\n"
                                   ")\n")
                           7z 7z 7z)))
                ((string-equal "minizip" zip)
                 (concat "REM minizip recursive call\n\n"
                         "call :loop %_ARGV%\n"
                         "goto :end\n"
                         "\n:zip\n"
                         "set _file=%1\n"
                         "set _file=%_file:./=%\n"
                         "if not \"%_file%\"==\"%_ZIP%\" (\n"
                         "  if exist %_ZIP% (\n"
                         "    minizip %_OPT% -a %_ZIP% %_file%\n"
                         "  ) else (\n"
                         "    minizip %_OPT% %_ZIP% %_file%\n"
                         "  )\n"
                         ")\n"
                         "goto :end\n"
                         "\n:loop\n"
                         "for %%i in (%*) do (\n"
                         "  if exist \"%%i/*\" (\n"
                         "    for %%f in (%%i/*) do (\n"
                         "      call :loop %%i/%%f\n"
                         "    )\n"
                         "    for /d %%d in (%%i/*) do (\n"
                         "      call :loop %%i/%%d\n"
                         "    )\n"
                         "  ) else (\n"
                         "    call :zip %%i\n"
                         "  )\n"
                         ")\n"
                         "\n:end\n"))))
         (v-home% ".exec/zip.bat"))))

    (unless% (executable-find% "zip")
      ;; zip external program
      ;; prefer 7z, because 7za less archive formats supported
      (cond ((executable-find% "7z") (_make_zip_bat_ "7z"))
            ((executable-find% "7za") (_make_zip_bat_ "7za"))
            ((executable-find% "minizip") (_make_zip_bat_ "minizip"))))))


(defun dired*-echo-current-directory (&optional localp)
  "Echo the current directory in \\=`dired-mode\\='."
  (interactive)
  (let ((d (dired-current-directory localp))
        (interprogram-paste-function nil))
    (kill-new d)
    (message "%s" d)))

(defun dired*-get-filename ()
  (if-fn% dired-get-file-for-visit dired
          (dired-get-file-for-visit)
    (or (dired-get-filename nil t)
        (user-error "%s" "No file on this line"))))

(when-platform% darwin
  (when-version% <= 28
    (defun browse-url-default-macosx-browser* (url &optional _)
      (interactive)
      (start-process (concat "open " url) nil "open" url))))

(defun dired*-browse-file ()
  "Browse the file using external browser."
  (interactive)
  (let ((browse-url-browser-function #'browse-url-default-browser)
        (name (if (eq 'dired-mode major-mode)
                  (dired*-get-filename)
                (buffer-file-name))))
    (and name
         (if-platform% darwin
             (let ((fn (symbol-function 'browse-url-default-macosx-browser)))
               (unwind-protect
                   (progn
                     (fset 'browse-url-default-macosx-browser
                           #'browse-url-default-macosx-browser*)
                     (browse-url name))
                 (fset 'browse-url-default-macosx-browser fn)))
           (browse-url name)))))


(defun dired*-hexl-find-file ()
  "Edit the current file as hex dump format in \\=`dired-mode\\='."
  (interactive)
  (hexl-find-file (dired*-get-filename)))

(defun dired*-copy-filename-as-kill (&optional arg)
  "See \\=`dired-copy-filename-as-kill\\='."
  (interactive "P")
  (let ((interprogram-paste-function nil))
    (dired-copy-filename-as-kill arg)))

(defun on-dired-init! ()
  "On \\=`dired\\=' initialization."
  (when-platform% windows-nt
    ;; prefer GNU find on Windows, such for `find-dired' or `find-name-dired'.
    (when% (let ((ver (shell-command* "find" "--version")))
             (and (zerop (car ver))
                  (string-match "^find (GNU findutils)" (cdr ver))))
      (windows-nt-env-path+ (file-name-directory (executable-find* "find")))))
  ;; keys
  (define-key dired-mode-map "b" #'dired*-hexl-find-file)
  (define-key dired-mode-map "B" #'dired*-browse-file)
  (define-key dired-mode-map "w" #'dired*-copy-filename-as-kill)
  (define-key dired-mode-map "W" #'dired*-echo-current-directory))

;; end of `dired' setting

;;; detect-coding-string

(when-platform% windows-nt

  (unless% (eq default-file-name-coding-system locale-coding-system)

    (defun insert-directory* (file switches &optional wildcard full-directory-p)
      "\\=`dired-find-file\\=' should failed when using GNU's ls program on Windows.
       We try to encode multibyte directory name with
       \\=`locale-coding-system\\=' when the multibyte directory name
       encoded with non \\=`locale-coding-system\\='."
      (let ((file (if (multibyte-string-p file)
                      (encode-coding-string file locale-coding-system)
                    file)))
        (funcall (symbol-function '_insert-directory_)
                 file switches wildcard full-directory-p)))

    (defun dired-shell-stuff-it* (command file-list on-each &optional _)
      "\\=`dired-do-shell-command\\=' or \\=`dired-do-async-shell-command\\='
       should failed when open the files which does not been
       encoded with \\=`locale-coding-system\\='."
      (let ((file-list (let ((arg1 file-list) (fs nil))
                         (dolist (x arg1 fs)
                           (append!
                            (if (multibyte-string-p x)
                                (encode-coding-string x locale-coding-system)
                              x)
                            fs t)))))
        (funcall (symbol-function '_dired-shell-stuff-it_)
                 command file-list on-each)))

    (defun dired-shell-command* (cmd)
      "\\=`dired-do-compress-to\\=' should failed when
       \\=`default-directory\\=' or \\=`dired-get-marked-files\\=' does not
       encoded with \\=`locale-coding-system\\='."
      (let ((cmd (if (multibyte-string-p cmd)
                     (encode-coding-string cmd locale-coding-system)
                   cmd)))
        (funcall (symbol-function '_dired-shell-command_) cmd)))

    (defun dired-compress-file* (file)
      "\\=`dired-compress-file\\=' should failed when FILE arg does not
       encoded with \\=`locale-coding-string\\='."
      (let ((file (if (multibyte-string-p file)
                      (encode-coding-string file locale-coding-system)
                    file)))
        (funcall (symbol-function '_dired-compress-file_) file)))))

;;

;;; `arc-mode'

(eval-when-compile
  (defmacro when-archive-summarize-files% (&rest body)
    (declare (indent 0))
    (when-fn% archive-summarize-files arc-mode
      `(unless-default-file-name-coding-system%
         ,@body))))

(when-archive-summarize-files%
  (defun archive-summarize-files* (files)
    "\\=`archive-summarize-files\\=' may not display file name."
    (let ((files (if (consp files)
                     (let ((fs nil))
                       (dolist (x files fs)
                         (when (and (arrayp x) (= 3 (length x)))
                           (let ((decode (substring-no-properties
                                          (decode-coding-string
                                           (aref x 0) locale-coding-system))))
                             (aset x 0 decode)
                             (aset x 2 (length decode))))
                         (append! x fs t)))
                   files)))
      (funcall (symbol-function '_archive-summarize-files_) files))))

(defun on-arc-mode-init! ()
  (when-archive-summarize-files%
    (defadvice* '_archive-summarize-files_
      'archive-summarize-files #'archive-summarize-files*)))

;; end of `arc-mode'

;;; `dired-aux'

(defun on-dired-aux-init! ()
  "On \\=`dired-aux\\=' initialization."
  ;; on ancient Emacs, `dired' can't recognize .zip archive.
  ;; [! zip x.zip ?] compress marked files to x.zip，
  ;; see `dired-compress-file-suffixes'.
  (when-var% dired-compress-files-suffixes dired-aux
    (when% (and (not (assoc-string "\\.zip\\'" dired-compress-file-suffixes))
                (executable-find* "zip")
                (executable-find* "unzip"))
      (push! '("\\.zip\\'" ".zip" "unzip") dired-compress-file-suffixes)))
  ;; uncompress/compress .7z file
  (when% (or (executable-find* "7z")
             (executable-find* "7za"))
    (let ((7za? (if% (executable-find* "7z") "7z" "7za")))
      (when-var% dired-compress-file-suffixes dired-aux
        ;; [Z] uncompress from .7z
        (let ((uncompress (concat 7za? " x -t7z -aoa -o%o %i")))
          (if% (assoc-string "\\.7z\\'" dired-compress-file-suffixes)
              (setcdr (assoc-string "\\.7z\\'" dired-compress-file-suffixes)
                      (list "" uncompress))
            (push! (list "\\.7z\\'" "" uncompress)
                   dired-compress-file-suffixes)))
        ;; [c] compress to .7z
        (when-fn% dired-do-compress-to dired-aux
          (let ((compress (concat 7za? " a -t7z %o %i")))
            (require 'format-spec)
            (if% (assoc-string "\\.7z\\'" dired-compress-files-alist)
                (setcdr (assoc-string "\\.7z\\'" dired-compress-files-alist)
                        compress)
              (push! (cons "\\.7z\\'" compress)
                     dired-compress-files-alist)))))))
  ;; error at `dired-internal-noselect' on Windows:
  ;; Reading directory: "ls --dired -al -- d:/abc/中文/" exited with status 2
  ;; https://lists.gnu.org/archive/html/emacs-devel/2016-01/msg00406.html
  ;; (setq file-name-coding-system locale-coding-system)
  (when-platform% windows-nt
    (unless% (eq default-file-name-coding-system locale-coding-system)
      (defadvice* '_insert-directory_ 'insert-directory #'insert-directory*)
      (defadvice* '_dired-shell-stuff-it_
        'dired-shell-stuff-it #'dired-shell-stuff-it*)
      (defadvice* '_dired-shell-command_
        'dired-shell-command #'dired-shell-command*))
    ;; [Z] to compress or uncompress .gz file
    (when-var% dired-compress-file-suffixes dired-aux
      (when% (or (executable-find% "gzip")
                 (executable-find% "7z")
                 (executable-find% "7za"))
        (when% (assoc-string ":" dired-compress-file-suffixes)
          (setq dired-compress-file-suffixes
                (remove (assoc-string ":" dired-compress-file-suffixes)
                        dired-compress-file-suffixes)))
        (when% (and (null (executable-find* "gunzip"))
                    (or (executable-find* "7z")
                        (executable-find* "7za")))
          (let ((7za? (concat (if (executable-find% "7z") "7z" "7za")
                              " x -tgz -aoa %i")))
            (if% (assoc-string "\\.gz\\'" dired-compress-file-suffixes)
                (setcdr (assoc-string "\\.gz\\'" dired-compress-file-suffixes)
                        (list "" 7za?))
              (push! (cons "\\.gz\\'" 7za?) dired-compress-file-suffixes))))

        (when-fn% dired-compress-file dired-aux
          (defadvice* 'dired-compress-file
            'dired-compress-file #'dired-compress-file*))))))

;; end of `dired-aux'

(provide 'direds)

;; end of direds.el
