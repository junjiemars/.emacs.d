;; -*- lexical-binding:t -*-
;;;;
;; Nore Emacs
;; https://github.com/junjiemars/.emacs.d
;;;;
;; cc.el
;;;;
;; Commentary: essential C programming
;;;;

;;; require

;; `newline*'
(require% 'ed (v-home%> "config/ed"))
;; `(tags-spec->% :root)'
(require% 'tags (v-home%> "config/tags"))
(require% 'ssh (v-home%> "config/ssh"))

;; end of require

;;;
;; msvc host environment
;;;

(when-platform% windows-nt
  (defun cc*-check-vcvarsall-bat ()
    "Return the path of vcvarsall.bat if which exists."
    (let* ((pfroot (posix-path (getenv "PROGRAMFILES")))
           (vsroot (concat pfroot " (x86)/Microsoft Visual Studio/"))
           (vswhere (concat vsroot "Installer/vswhere.exe")))
      (when (file-exists-p vswhere)
        (posix-path
         (or (let* ((rc (shell-command* (shell-quote-argument vswhere)
                          "-nologo -latest -property installationPath"))
                    (bat (and (zerop (car rc))
                              (concat
                               (string-trim> (cdr rc))
                               "/VC/Auxiliary/Build/vcvarsall.bat"))))
               (when (file-exists-p bat) bat))
             (let* ((ver (car (directory-files
                               vsroot
                               t "[0-9]+" #'string-greaterp)))
                    (bat (concat
                          ver
                          "/BuildTools/VC/Auxiliary/Build/vcvarsall.bat")))
               (when (file-exists-p bat) bat))))))))

(when-platform% windows-nt
  (defun platform-arch ()
    "Return platform architecture."
    (let ((d (strchr system-configuration ?-)))
      (if (and d (> d 0))
          (downcase (substring-no-properties system-configuration 0 d))
        (let ((p (when-platform% windows-nt
                   (getenv-internal "PROCESSOR_ARCHITECTURE"))))
          (if p (downcase p)
            (let ((m (shell-command* "uname -m")))
              (and (zerop (car m))
                   (downcase (string-trim> (cdr m)))))))))))

(when-platform% windows-nt
  (defun cc*-make-env-bat ()
    "Make cc-env.bat in \\=`exec-path\\='."
    (let ((vcvarsall (cc*-check-vcvarsall-bat))
          (arch (platform-arch)))
      (when (and vcvarsall arch)
        (save-str-to-file
         (concat "@echo off\n"
                 (concat "rem generated by Nore Emacs" (nore-emacs) "\n\n")
                 "pushd %cd%\n"
                 "cd /d \"" (file-name-directory vcvarsall) "\"\n"
                 "\n"
                 "call vcvarsall.bat " arch "\n"
                 "set CC=cl" "\n"
                 "set AS=ml" (if (string-match "[_a-zA-Z]*64" arch)
                                 "64"
                               "")
                 "\n\n"
                 "popd\n"
                 "echo \"%INCLUDE%\"\n")
         (v-home% ".exec/cc-env.bat"))))))

;; msvc host environment

;;;
;; CC
;;;

(defun cc*-cc-check ()
  (let ((cx (if-platform% windows-nt
                '("cc-env.bat" "cl" "gcc")
              '("cc" "gcc" "clang")))
        (o (make-temp-file "cc-c-" nil
                           (if-platform% windows-nt
                               ".exe"
                             ".out")))
        (i (save-str-to-file
            (concat
             "int main(void) {\n"
             "  return 0;\n"
             "}")
            (make-temp-file "cc-s-" nil ".c"))))
    (catch 'br
      (dolist (cc cx)
        (let ((rc (shell-command*
                      (format (if-platform% windows-nt
                                  (if (string-equal "cc-env.bat" cc)
                                      (concat "%s %s -Fe%s -Fo")
                                    "%s %s -o%s")
                                "%s %s -o%s")
                              (if-platform% windows-nt
                                  (if (string-equal "cc-env.bat" cc)
                                      "cc-env.bat && cl"
                                    cc)
                                cc)
                              i o))))
          (when (zerop (car rc))
            (throw 'br cc)))))))

(defalias 'cc*-cc
  (let ((b (cc*-cc-check)))
    (lambda (&optional n)
      (if (null n) b (setq b n))))
  "The name of C compiler executable.")

;; end of CC

;;;
;; xargs
;;;

(when-platform% windows-nt
  (defun cc*-make-xargs-bin ()
    "Make a GNU's xargs alternation in \\=`exec-path\\='."
    (let* ((c (make-temp-file "cc-xargs-" nil ".c"))
           (exe (v-home% ".exec/xargs.exe"))
           (cc (concat (cc*-cc)
                       " -nologo -DNDEBUG=1 -O2 -utf-8"
                       " " c
                       " -Fo" temporary-file-directory
                       " -Fe" exe
                       " -link -release")))
      (when (save-str-to-file
             (concat "#include <stdio.h>\n"
                     "int main(int argc, char **argv) {\n"
                     "  int ch;\n"
                     "  while (EOF != (ch = fgetc(stdin))) {\n"
                     "    fputc(ch, stdout);\n"
                     "  }\n"
                     "  if (ferror(stdin)) {\n"
                     "    perror(\"read failed from stdin\");\n"
                     "    return 1;\n"
                     "  }\n"
                     "  return 0;\n"
                     "}\n")
             c)
        (let ((rc (shell-command* cc)))
          (when (zerop (car rc))
            (file-name-nondirectory exe)))))))

(when-platform% windows-nt
  (defconst +cc*-xargs-bin+
    (file-name-nondirectory
     (or (executable-find%
          "xargs"
          (lambda (xargs)
            (let ((x (shell-command* "echo xxx && echo zzz|xargs -0")))
              (and (zerop (car x))
                   (string-match "^zzz" (cdr x))))))
         (and (cc*-cc) (cc*-make-xargs-bin))))
    "The name of xargs executable."))

;; end of xargs

;;;
;; #include
;;;

(defun cc*-include-check (&optional remote)
  "Return a list of system cc include path."
  (let ((rc (if remote
                (shell-command* "ssh"
                  (concat (ssh-remote->user@host remote)
                          " \"echo ''|cc -v -E 2>&1 >/dev/null -\""))
              (if-platform% windows-nt
                  (shell-command* (cc*-cc))
                ;; Darwin/Linux
                (shell-command* "echo ''|" (cc*-cc)
                                " -v -E 2>&1 >/dev/null -")))))
    (when (zerop (car rc))
      (let ((pre (cdr rc)) (inc nil) (beg nil))
        (if-platform% windows-nt
            (dolist (x (var->paths
                        (car
                         (nreverse (split-string* pre "\n" t "[ \"]*"))))
                       (nreverse inc))
              (setq inc (cons (posix-path x) inc)))
          ;; Darwin/Linux
          (catch 'br
            (dolist (x (split-string* pre "\n" t "[ \t\n]"))
              (when (and beg (string-match "End of search list\\." x))
                (throw 'br (nreverse inc)))
              (when beg (setq inc (cons x inc)))
              (when (and (null beg)
                         (string-match
                          "#include <\\.\\.\\.> search starts here:" x))
                (setq beg t)))))))))

(defun cc*-system-include-read (file &optional remote)
  (or (read-sexp-from-file file)
      (let ((xs nil))
        (dolist (x (cc*-include-check remote) (nreverse xs))
          (let ((x1 (if remote (concat remote x) x)))
            (and (file-exists-p x1)
                 (setq xs (cons x1 xs))))))))

(defun cc*-system-include-file (&optional remote)
  (let* ((ss (if remote
                 (intern (mapconcat #'identity
                                    (ssh-remote->ids remote)
                                    "-"))
               'native))
         (fs (format "%s-%s.el" (v-home% ".exec/cc-inc") ss)))
    (cons ss fs)))

(defalias 'cc*-system-include
  (let ((dx nil))
    (lambda (&optional op remote)
      (let* ((ssfs (cc*-system-include-file remote))
             (ss (car ssfs)) (fs (cdr ssfs)))
        (cond ((and op (eq op :read))
               (let ((r (cc*-system-include-read fs remote)))
                 (plist-get (setq dx (plist-put dx ss r)) ss)))
              ((and op (eq op :save)) (save-sexp-to-file (plist-get dx ss) fs))
              (t (plist-get dx ss))))))
  "Return a list of system include directories.\n
Load \\=`cc*-system-include\\=' from file when CACHED is t,
otherwise check cc include on the fly.
The REMOTE argument from \\=`ssh-remote-p\\='.")

(eval-when-compile
  (defmacro when-fn-ff-find-other-file% (&rest body)
    (if-fn% ff-find-other-file find-file
            `(progn% ,@body)
      `(comment ,@body))))

(when-fn-ff-find-other-file%
 (defun cc*-find-include-file (&optional in-other-window)
   "Find C include file in \\=`cc*-system-include\\=' or specified directory."
   (interactive "P")
   (setq% cc-search-directories
          (let ((file (buffer-file-name (current-buffer))))
            (nconc (when (stringp file)
                     (let ((pwd (file-name-directory file)))
                       (list (string-trim> pwd "/")
                             (string-trim> (path- pwd) "/"))))
                   (cc*-system-include :read (ssh-remote-p file))))
          find-file)
   (when-fn% xref-push-marker-stack xref
     (autoload 'xref-push-marker-stack "xref")
     (xref-push-marker-stack))
   (ff-find-other-file in-other-window nil)))

;; end of #include

;;;
;; #define
;;;

(when-platform% windows-nt
  (defun cc*-make-macro-dump-bin (&optional options)
    "Make cc-dump-macro.exe for printing predefined macros."
    (let ((c (v-home% ".exec/cc-dump-macro.c"))
          (exe (v-home% ".exec/cc-dump-macro.exe")))
      (unless (file-exists-p c)
        (copy-file (emacs-home% "config/sample-cc-dump-macro.c") c))
      (let ((cmd (shell-command* (cc*-cc)
                   (concat " -nologo"
                           " " options
                           " " c
                           " -Fo" temporary-file-directory
                           " -Fe" exe))))
        (when (zerop (car cmd))
          (file-name-nondirectory exe))))))


(defun cc*-dump-predefined-macros (&optional options)
  "Dump predefined macros."
  (interactive "sInput C compiler's options: ")
  (let* ((remote (ssh-remote-p (buffer-file-name (current-buffer))))
         (cc (if remote "cc" (cc*-cc)))
         (opts  (format "%s -dM -E -" options))
         (rc (cond (remote (fluid-let (shell-file-name "sh")
                             (shell-command* "ssh"
                               (ssh-remote->user@host remote)
                               cc opts)))
                   (t (if-platform% windows-nt
                          (cc*-make-macro-dump-bin options)
                        (shell-command* cc opts))))))
    (with-current-buffer
        (switch-to-buffer
         (format "*Macro Predefined%s*"
                 (if remote
                     (concat "@" (ssh-remote->user@host remote))
                   "")))
      (view-mode -1)
      (erase-buffer)
      (insert (if (zerop (car rc))
                  (if (> (length (cdr rc)) 0)
                      (cdr rc)
                    "/* C preprocessor no output! */")
                (cdr rc)))
      (c-mode)
      (goto-char (point-min))
      (view-mode 1))))

;; end of #define

;;;
;; `tags'
;;;

(defun cc*-make-tags (&optional renew)
  "Make system C tags."
  (let ((file (concat (tags-spec->% :root) "os.TAGS"))
        (opt "--langmap=c:.h.c --c-kinds=+ptesgux --extra=+fq"))
    (cond (renew (let ((inc (cc*-system-include :read)))
                   (make-c-tags (car inc) file opt nil nil t)
                   (dolist (p (cdr inc) file)
                     (make-c-tags p file opt))))
          (t file))))

;; end of `cc*-make-tags'

;;;
;; `cc-styles'
;;;

(defun cc*-style-arglist-cont-nonempty (langem)
  (let ((col (save-excursion
               (goto-char (cdr langem))
               (current-column))))
    (cond ((= col 0) 'c-basic-offset)
          (t 'c-lineup-arglist))))

(when-fn% align-entire align
  (defun cc*-style-align-entire (&rest _)
    "See \\=`align-entire\\='."
    (interactive)
    (let ((align-default-spacing 2))
      (ignore* align-default-spacing)
      (call-interactively #'align-entire))))

(when-fn% c-backslash-region cc-cmds
  (defun cc*-style-align-backslash (&rest _)
    "See \\=`c-backslash-region\\='."
    (interactive)
    (let ((c-backslash-column 48)
          (c-backslash-max-column 72))
      (ignore* c-backslash-column c-backslash-max-column)
      (call-interactively #'c-backslash-region))))

;; end of `cc-styles'

;;;
;; `cmacexp'
;;;

(eval-when-compile
  (defmacro when-c-macro-expand% (&rest body)
    (declare (indent 0))
    (if-fn% c-macro-expand cmacexp
            `(progn% ,@body)
      `(comment ,@body))))

(when-c-macro-expand%
 (defun cc*-macro-expand (&rest args)
   "Macro expanding current buffer."
   (interactive "r\nP")
   (let ((remote (ssh-remote-p (buffer-file-name (current-buffer)))))
     (setq% c-macro-prompt-flag t cmacexp)
     (setq% c-macro-buffer-name
            (if remote
                (format "*Macro Expanded@%s*"
                        (ssh-remote->user@host remote))
              "*Macro Expanded*")
            cmacexp)
     (setq% c-macro-preprocessor
            (if remote
                (format "ssh %s %s"
                        (ssh-remote->user@host remote)
                        "cc -E -o - -")
              (if-platform% windows-nt
                  ;; cl.exe can't compile on the fly without xargs
                  (let ((tmp (make-temp-file "cc-m-" nil ".c")))
                    (format "%s -0 > %s && %s && cl -E %s"
                            +cc*-xargs-bin+ tmp
                            (cc*-cc) tmp))
                (format "%s -E -o - -" (cc*-cc))))
            cmacexp)
     (apply #'c-macro-expand args))))

;; end of `cmacexp'

;;;
;; format
;;;

(defun cc*-format-region (&optional beg end)
  "Format the region in (BEG,END) of current buffer via clang-format."
  (interactive (if-region-active
                   (list (region-beginning) (region-end))
                 (list (point-min) (point-max))))
  (let* ((buf (current-buffer))
         (tmp (get-buffer-create* (symbol-name (gensym* "cc-fmt-")) t))
         (cur (with-current-buffer buf (point))))
    (unwind-protect
        (let ((rc (call-process-region
                   nil nil "clang-format" nil tmp nil
                   "-output-replacements-xml"
                   "-fallback-style" "gnu"
                   "-offset" (number-to-string (1- (position-bytes beg)))
                   "-length" (number-to-string
                              (- (position-bytes end) (position-bytes beg)))
                   "-cursor" (number-to-string (1- (position-bytes cur)))))
              (c1 cur)
              (xml `(("&lt;"   . "<")
                     ("&gt;"   . ">")
                     ("&apos;" . "'")
                     ("&quot;" . "\"")
                     ("&amp;"  . "&")
                     ("&#\\([0-9]+\\);" . 10)
                     ("&#[xX]\\([0-9a-fA-F]+\\);" . 16))))
          (when (and rc (= rc 0))
            (with-current-buffer tmp
              (goto-char (point-min))
              (when (re-search-forward "incomplete_format='false'" nil t 1)
                (when (re-search-forward
                       "<cursor>\\([0-9]+\\)</cursor>" nil t 1)
                  (setq c1 (string-to-number
                            (buffer-substring-no-properties
                             (match-beginning 1) (match-end 1)))))
                (goto-char (point-max))
                (while (re-search-backward
                        (concat
                         "<replacement offset='\\([0-9]+\\)'"
                         " length='\\([0-9]+\\)'>\\(.*?\\)"
                         "</replacement>")
                        nil t)
                  (let ((off (string-to-number
                              (buffer-substring-no-properties
                               (match-beginning 1) (match-end 1))))
                        (len (string-to-number
                              (buffer-substring-no-properties
                               (match-beginning 2) (match-end 2))))
                        (txt (buffer-substring-no-properties
                              (match-beginning 3) (match-end 3))))
                    (with-current-buffer buf
                      (let ((lhs (byte-to-position (1+ off)))
                            (rhs (byte-to-position (1+ (+ off len)))))
                        (when (and (<= beg lhs) (>= end rhs))
                          (and (< lhs rhs) (delete-region lhs rhs))
                          (when txt
                            (goto-char lhs)
                            (insert (strawk txt xml))))))))
                (and c1 (setq c1 (byte-to-position c1)))))
            (goto-char (setq cur (or c1 cur)))))
      (and tmp (kill-buffer tmp))
      cur)))

;; end of format

;;;
;; `man'
;;;

(defun on-man-init! ()
  "On \\=`man\\=' initialization."
  (when-platform% darwin
    (setq% manual-program "/usr/bin/man" man))
  ;; fix cannot find include path in `Man-mode'
  (setq% Man-header-file-path (cc*-system-include :read) man))

;; end of `man'

;;;
;; keys
;;;

(defun cc*-define-keys (keymap)
  ;; dump predefined macros
  (define-key keymap "#" #'cc*-dump-predefined-macros)
  ;; raw newline
  (define-key keymap (kbd% "RET") #'newline*)
  ;; align entire style
  (when-fn% align-entire align
    (define-key keymap "|" #'cc*-style-align-entire))
  ;; align backslash style
  (when-fn% c-backslash-region cc-cmds
    (autoload 'c-backslash-region "cc-cmds")
    (define-key keymap "" #'cc*-style-align-backslash))
  (when-fn-ff-find-other-file%
   (define-key keymap "fi" #'cc*-find-include-file))
  (when-c-macro-expand%
   (define-key keymap "" #'cc*-macro-expand))
  (define-key keymap (kbd% "C-c M-c f") #'cc*-format-region))

;; end of keys

;;;
;; `cc-mode'
;;;

(defun on-cc-mode-init! ()
  "On \\=`cc-mode\\=' initialization."
  ;; indent line or region
  (when-fn% c-indent-line-or-region cc-cmds
    (define-key% c-mode-map (kbd "TAB") #'c-indent-line-or-region))
  ;; `subword-mode'
  (if-fn% subword-mode subword
          (define-key% c-mode-map "" #'subword-mode)
    (define-key% c-mode-map "" #'c-subword-mode))
  (when (boundp 'c-mode-map)
    (cc*-define-keys c-mode-map))
  (when (boundp 'c++-mode-map)
    (cc*-define-keys c++-mode-map)))

;; end of `cc-mode'

;;;
;; `c-ts-mode'
;;;

(when-feature-treesit%
  (defun on-c-ts-mode-init! ()
    "On \\=`c-ts-mode\\=' initialization."
    (when (boundp 'c-ts-mode-map)
      (cc*-define-keys c-ts-mode-map))
    (when (boundp 'c++-ts-mode-map)
      (cc*-define-keys c++-ts-mode-map))))

;; end of `c-ts-mode'

(provide 'cc)

;; end of cc.el
